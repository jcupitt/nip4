marshal = gnome.genmarshal(
  'nip4marshal',
  prefix: 'nip4',
  sources: 'nip4marshal.list',
)

resources = gnome.compile_resources(
  'nip4-gresources',
  'gtk/nip4.gresources.xml',
  source_dir: 'gtk',
)

# compile in an icon for a windows exe
if host_machine.system() == 'windows'
  windows = import('windows')
  resources += windows.compile_resources(
      'nip4.rc',
      depend_files: 'nip4.ico',
  )
endif

# headers for enum scanning
headers = files(
  'tilesource.h',
  'tilecache.h',
)

sources = files(
  'action.c',
  'app.c',
  'builtin.c',
  'class.c',
  'classmodel.c',
  'colour.c',
  'colourview.c',
  'column.c',
  'columnview.c',
  'compile.c',
  'displaybar.c',
  'dump.c',
  'editview.c',
  'expr.c',
  'expression.c',
  'expressionview.c',
  'filemodel.c',
  'fontname.c',
  'fontnameview.c',
  'formula.c',
  'fuzzy.c',
  'graphicview.c',
  'group.c',
  'gtkutil.c',
  'heap.c',
  'heapmodel.c',
  'iarrow.c',
  'icontainer.c',
  'ientry.c',
  'iimage.c',
  'iimageview.c',
  'imagedisplay.c',
  'imageinfo.c',
  'imageui.c',
  'imagewindow.c',
  'infobar.c',
  'iobject.c',
  'iregion.c',
  'iregiongroup.c',
  'iregiongroupview.c',
  'iregionview.c',
  'itext.c',
  'itextview.c',
  'itextview.c',
  'kplot/array.c',
  'kplot/border.c',
  'kplot/bucket.c',
  'kplot/buffer.c',
  'kplot/colours.c',
  'kplot/draw.c',
  'kplot/grid.c',
  'kplot/hist.c',
  'kplot/kdata.c',
  'kplot/kplot.c',
  'kplot/label.c',
  'kplot/margin.c',
  'kplot/mean.c',
  'kplot/plotctx.c',
  'kplot/reallocarray.c',
  'kplot/stddev.c',
  'kplot/tic.c',
  'kplot/vector.c',
  'link.c',
  'log.c',
  'main.c',
  'mainwindow.c',
  'managed.c',
  'managedfile.c',
  'managedgobject.c',
  'managedstring.c',
  'matrix.c',
  'matrixview.c',
  'model.c',
  'number.c',
  'numberview.c',
  'option.c',
  'optionview.c',
  'path.c',
  'pathname.c',
  'pathnameview.c',
  'plot.c',
  'plotdisplay.c',
  'plotview.c',
  'plotwindow.c',
  'predicate.c',
  'prefcolumnview.c',
  'prefs.c',
  'prefworkspaceview.c',
  'program.c',
  'progress.c',
  'properties.c',
  'real.c',
  'recover.c',
  'reduce.c',
  'regionview.c',
  'rhs.c',
  'rhsview.c',
  'row.c',
  'rowview.c',
  'saveoptions.c',
  'secret.c',
  'slider.c',
  'sliderview.c',
  'spin.c',
  'string.c',
  'stringview.c',
  'subcolumn.c',
  'subcolumnview.c',
  'symbol.c',
  'tile.c',
  'tilecache.c',
  'tilesource.c',
  'toggle.c',
  'toggleview.c',
  'tool.c',
  'toolkit.c',
  'toolkitgroup.c',
  'toolkitgroupview.c',
  'toolkitview.c',
  'toolview.c',
  'trace.c',
  'tree.c',
  'tslider.c',
  'util.c',
  'value.c',
  'valueview.c',
  'vector.c',
  'view.c',
  'vipsobject.c',
  'vobject.c',
  'workspace.c',
  'workspacedefs.c',
  'workspacegroup.c',
  'workspacegroupview.c',
  'workspaceroot.c',
  'workspaceview.c',
  'workspaceviewlabel.c',
)

parse_c = custom_target('parse.c',
  output: ['parse.c', 'parse.h'],
  input: 'parse.y',
  command: [yacc,
    '--output=@OUTPUT0@',
    '--header=@OUTPUT1@',
    '@INPUT@',
  ],
)

lex_c = custom_target('lex.c',
  output: 'lex.c',
  input: 'lex.l',
  command: [lex,
    '--outfile=@OUTPUT0@',
    '@INPUT@',
  ],
)

enumtypes = gnome.mkenums(
  'enumtypes',
  sources: headers,
  h_template: 'enumtypes.h.in',
  c_template: 'enumtypes.c.in',
)

nip4_lib = static_library('nip4',
  sources: [enumtypes, marshal, resources, parse_c, lex_c, sources],
  dependencies: nip4_deps,
)

nip4_dep = declare_dependency(
  sources: enumtypes[1],
  dependencies: nip4_deps,
  link_whole: nip4_lib,
)

nip4 = executable('nip4',
  'main-gui.c',
  dependencies: nip4_dep,
  win_subsystem: 'windows',
  install: true,
)

nip4_batch = executable('nip4-batch',
  'main-batch.c',
  dependencies: nip4_dep,
  win_subsystem: 'console',
  install: true,
)
